!!++
!! UNDERSAMPLING_BEAMS.INR
!! Title: Undersampling with Too Few Beams
!! Category: Simple Problem
!! Keywords: Wave, SPREAD, beams, undersampling, FOCUS
!! Description: Shows results of an undersampled mirror at
!! focus. Compares a 9X9 grid to a 41X41 grid of beams.
!! Edit History: (latest first)
!! 02/28/2002 - cp - corrected style
!! 03/16/2000 - cp - Reformatted; added header
!! 03/02/2000 - rh - creation
!!--

SYSTEM NEW
RESET

UNITS MM

SURFACE
  OPTICAL Z 7.5 -15 0 ELLIPSE 3.75
OBJECT 'MIRROR'
  INTERFACE 1 0  AIR AIR 
  REDEFINE COLOR 1

SURFACE 
  PLANE Z 0 RECTANGLE 0.2
OBJECT 'DETECTOR'
  INTERFACE 0 0 AIR AIR
  REDEFINE COLOR 1

PARABASALS 4
BEAMS COHERENT DIFFRACT
WAVELENGTH 1 UM
WIDTHS 1.6

GRID ELLIPSE Z -0.1 -4@3.75 9 9  !! Grid at -0.1 for art's sake
  SOURCE DIRECTION 0 0 1 

PIXELS 101
WINDOW Y  X 
SPREAD NORMAL
DISPLAY 
  ISOMETRIC 'INITIAL SOURCE FOR 9 X 9 BEAMS - BEAMS TOO LARGE FOR MIRROR'
RETURN

WINDOW Y Z
PLOT FACETS 5 5 OVERLAY

MISSED ARROW 10 'PROFILE WITH 10 X 10 BEAM RAY TRACE FOCUSED BY MIRROR NEAR DETECTOR' OVERLAY
TRACE PLOT COLOR 2
PIXELS 101
WINDOW X -2@1E-2 Y -2@1E-2
CONSIDER ONLY DETECTOR
FOCUS MOVE
SPREAD NORMAL
DISPLAY 
  ISOMETRIC 'IMAGE FOR 9 X 9 BEAMS - BEAMS TOO LARGE FOR MIRROR' 
RETURN

CONSIDER ALL
RAYS 0  !! Clear rays
GRID ELLIPTIC Z 7 -4@3.75 41 41  !! Start grid closer to mirror
  SOURCE DIRECTION 0 0 1

WINDOW Y X 
SPREAD NORMAL
DISPLAY 
  ISOMETRIC 'INITIAL SOURCE FOR 41 X 41 BEAMS - BEAM SIZE CORRECT FOR MIRROR'
RETURN

WINDOW X -2@1E-2 Y -2@1E-2
TRACE

CONSIDER ONLY DETECTOR
FOCUS MOVE
SPREAD NORMAL
DISPLAY 
  ISOMETRIC 'IMAGE FOR 41 X 41 BEAMS - BEAM SIZE IS CORRECT FOR MIRROR'   
RETURN

