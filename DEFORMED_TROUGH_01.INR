!!++
!! DEFORMED_TROUGH_01.INR
!! Title: Sinusoidal Deformation of a Parabolic Trough Reflector
!! Category: Simple Problem
!! Keywords: Geometry, DEFORM, ASYM, $FCN, trough, parabolic, reflector
!! Description: This example shows relatively large 
!! deformations easily seen in PLOT FACETS and the 3D Viewer.
!! Edit History: (latest first)
!! 06/02/2008 - cp - added header
!! 05/29/2008 - jah - creation
!!--
SYSTEM NEW
RESET 

UNITS M

PI=4*ATAN(1)

!! reflector optical properties
COATING PROPERTIES
  0.94 0 'REFL'

MODELS
  HARVEY 0.2 -1.6 0.3 !! PLOT
  
!! reflector geometrical parameters
LREFL=3        !! reflector trough length
WREFL=2        !! reflector trough width
FREFL=0.6      !! reflector trough focal length
RREFL=2*FREFL  !! reflector trough base radius of curvature

!! calculate parabolic edge sag of trough
SAGREFL=(((WREFL/2)^2)/RREFL)/(1+SQRT[1-(((WREFL/2)/RREFL)^2)])
&REG SAGREFL

!! define a perturbation function for the reflector
!!  > sinusoidal across the figured (width) direction
$FCN SINU 0.02*RREFL*COS(50*_1/WREFL)

!! define the trough reflector
SURFACE
  USERFUNC EXPLICIT 0 0 0 SINU                    !! perturbation function
  OPTICAL Z 0 (RREFL) -1 RECT (WREFL/2) (LREFL/2) !! base paraboloid 
  ASYM 1 0                                        !! make it a surface of extrusion rather than revolution
OBJECT
  .1 'REFLECTOR'                                  !! make it an object
      DEFORM .2                                   !! perturb by deformation
      INTERFACE COAT REFL AIR AIR                 !! give it reflectance
       SCATTER MODEL 1                            !! assign scattering behavior
         TOWARDS SPEC 11 PI/3 0 
      REDEFINE COLOR 25
RETURN

PLOT FACETS 51 51 0 ZX 0 (SAGREFL) 2@0 'Reflector cross section view'
$VIEW

