!!++
!! APODIZE_BY_HAND.INR
!! Title: Apodizing with Hand-Made Display File 
!! Category: Simple Problem
!! Keywords: Radiometry, apodizing, hand, COMBINE, DISPLAY, distribution, SPOTS, WRITE
!! Description: How to create a display file with user
!! supplied data. The format shown below must be followed
!! exactly.  The "DISPLAY -9" tells ASAP to rewind the BRO009.DAT
!! file and begin writing with the following format and data.
!! In the example, since X is given first, the display data
!! will be in the Window of Y-Z. The value of X is then irrelevant.
!! The data that follows may be replaced with a $READ command
!! followed by the file name containing the data.
!! Edit History: (latest first)
!! 04/10/2008 - cp - removed $READ keyword
!! 03/23/2006 - cp - added comments
!! 10/04/2002 - cp - added $FAST comment
!! 12/19/2001 - cp - fixed PLANE syntax
!! 02/09/2000 - cp - creation
!!--

SYSTEM NEW
RESET

UNITS MM
YMIN=-2
YMAX=2
ZMIN=-2
ZMAX=2

SURFACE
  PLANE X 0 RECT 2 2
OBJECT 'TARGET'

EMITTING RECT X -5 -2@2 10000 
  FLUX TOTAL 1
TRACE
CONSIDER ONLY TARGET
WINDOW Y (YMIN) (YMAX) Z (ZMIN) (ZMAX)
PIXELS 6  !! To match distribution file size below
SPOTS POSITION  !! Create display file to be used later
DISPLAY  !! Brings up BRO009.DAT by default
  PICTURE   'Original Source File'
  ISOMETRIC 'Original Source File'
  WRITE RESULT DIS  !! Write original distribution to file

!! Hand-made apodization
DISPLAY -9  !! Create your own distribution file - gets saved in BRO009.DAT
X 0 SPOTS Y (YMIN) (YMAX) 6 Z (ZMIN) (ZMAX) 6  !! X position (0) not important
0.7 0.8 0.6 0.5 0.4 0.3  !! SPOTS POS is assumed due to XYZ letters used
0.9 1.0 0.9 0.8 0.5 0.2  !! as opposed to ABC for SPOTS DIR. Y rows, Z columns.
0.6 0.6 0.5 0.4 0.2 0.5  !! Data can be brought in with $READ.
0.5 0.4 0.3 0.5 0.7 0.8  !! Use $FAST in first line of external file
0.3 0.2 0.1 0.3 0.5 0.6  !! to exceed 344 char limit, if necessary.
0.5 0.6 0.8 0.6 0.4 0.3

DISPLAY  !! Brings up new BRO009.DAT
  PICTURE    'Hand Made Display file'
  ISOMETRIC  'Hand Made Display file' 
  $FCN MULT (_1*_2)
  COMBINE RESULT MULT !! Multiply RESULT by hand-made apodization 
  WRITE RESULT DIS    !! and save in RESULT.

DISPLAY RESULT
  PICTURE   'Apodized by Hand'
  ISOMETRIC 'Apodized by Hand' 
RETURN
 