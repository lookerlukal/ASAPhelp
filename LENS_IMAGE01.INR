!!++
!! LENS_IMAGE01.INR
!! Title: Landscape Lens using IMAGE
!! Category: Simple Problem
!! Keywords: Geometry, IMAGE, LENSES, SINGLET, pupil
!! Description: Using the IMAGE command to image rays
!! through a lens and a pupil. It is used as a source
!! modifier.
!! Edit History: (latest first)
!! 02/16/2000 - cp - modified format; added description
!! 03/29/1998 - kg - creation
!!--

SYSTEM NEW
RESET

UNITS IN
WAVELENGTH 576.5 NM 

LENSES
  SINGLET Z 0 .5 1.5 BK7 RD 3.24794 11.42904
  OBJECT 'LANDSCAPE_LENS'

SURFACE
  PLANE Z 3.5 ELLIPSE 2@1.5 0.3/1.5  !! Cut hole inside
  OBJECT 'PHYSICAL_STOP'

SURFACE
  PLANE Z 8.5 ELLIPSE 2
  OBJECT 'FILM_PLANE'

!! Entrance pupil located 5.639 inches from 1st surface
!! Entrance pupil is 1 inch in diameter
!! 10 degrees off-axis 

BEAMS INCOHERENT GEOMETRIC
GRID RECTANGLE Z 3.5 -4@0.3 1 10 
  IMAGE 1 3 1  !! Maps the GRID of rays through the stop
               !! position into the lens object space
               !! to create an efficient raytrace.
  SOURCE DIRECTION 0 SIN[10] COS[10] 
  MOVE TO Z -1

WINDOW Y Z
PROFILES OVERLAY
PLOT RAYS 10  !! Ray lengths represent intensity, normalized to 10
PLOT FACETS 5 5 OVERLAY
!!PROFILES OVERLAY
TRACE PLOT
RETURN