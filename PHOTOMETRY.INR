!!++
!! PHOTOMETRY.INR
!! Title: 'Radiometric Analysis' Appendix File 
!! Category: Simple Problem
!! Keywords: Radiometry, cie, blackbody, THERMAL, spectral, photopic, scotopic, eye, color
!! Description: This file is from the appendix of the
!! Radiometric Analysis (BRO0909.pdf) Tech Guide.  It has 
!! not been altered from its original form, and may be used
!! as a working reference for this guide.  NOTE: It must be
!! run with the companion CIE 1964 Standard 10 degree color
!! coordinate file called CIE10D.INR.  This file should be located
!! in the ASAP Examples folder.  Be sure to put it in your
!! ASAP working directory before running this (PHOTOMETRY) file. 
!! The following notes are taken from the guide regarding this file:
!!
!! The spectral power distribution is used, and not the spectral
!! power density.  A blackbody emitting at 6500K is used as the
!! source.  The file also contains a method for computing CIE
!! color coordinates, with the CIE 1964 Standard Observer
!! observing a field of view greater than four degrees.
!! In this case, the computed CIE chromaticity coordinates
!! should closely match those of a D65 standard illuminant.  The
!! D65 standard illuminant has a correlated color temperature of
!! 6500K.  The correlated color temperature of a source is the
!! absolute temperature of the blackbody, the color of which most
!! nearly matches the color of the source.
!! Edit History: (latest first)
!! 01/06/2005 - cp - corrected description
!! 11/18/2003 - cp - fixed keyword
!! 03/06/2003 - cp - added header
!! 10/10/2002 - kg - creation
!!--

SYSTEM NEW
RESET

!!ASAP PHOTOMETRY CALCULATIONS

SYSTEM NEW
RESET
RAYS 0

UNITS MM 'lm' lumens
WAVELENGTH 0.555 UM

!!SET UP BLACKBODY EMITTER
TEMP=6500 !!DAYLIGHT D65 SOURCE
DLAMBDA=5

BLACKBODY {
!!SETS UP A BLACKBODY THERMAL EMITTER AT TEMPERATURE TEMP
!!ALSO CREATES A CURVE OF THE SPECTRAL APODIZATION
SPECTRUM THERMAL (TEMP) 
XMEMORY MIN
SEED 200000001 QUASI
$DO 380 780 (DLAMBDA)
 { 
  WAVELENGTH ?/1000 UM
  EMITTING RECTANGLE Z 0 2@0.5 10
 } 
FLUX TOTAL 10 watts
$ITER WAVE 380 780 -((780-380)/DLAMBDA+1) BBPOWER
 {
  SELECT ONLY SOURCE (WAVE-380)/DLAMBDA+1 
  STATS
  $GRAB 'TOTAL' 0 2 BBPOWER
 }
SELECT ALL
}

VISIBILITY { 1
$IF #1 EQS PHOTOPIC THEN
 $ITER WAVE 0.380 0.780 -((780-380)/DLAMBDA+1) VISIBILITY
  VISIBILITY=EYE(WAVE)
$ELSEIF #1 EQS SCOTOPIC THEN
 $ITER WAVE 0.380 0.780 -((780-380)/DLAMBDA+1) VISIBILITY
  VISIBILITY=EYE[WAVE]
$ELSE
 $ITER WAVE 0.380 0.780 -((780-380)/DLAMBDA+1) VISIBILITY
  VISIBILITY=EYE(WAVE)
  $PLOT OFF
  DISPLAY VISIBILITY
  GRAPH 'PHOTOPIC RESPONSE'
  'WAVELENGTH IN UM
 $ITER WAVE 0.380 0.780 -((780-380)/DLAMBDA+1) VISIBILITY
  VISIBILITY=EYE[WAVE]
  $PLOT NORM
  DISPLAY VISIBILITY
  GRAPH APPEND 'PHOTOPIC AND SCOTOPIC RESPONSE'
  'WAVELENGTH IN UM
  'CURVE 1 IS PHOTOPIC RESPONSE (SOLID LINE)
  'CURVE 2 IS SCOTOPIC RESPONSE (DOTTED LINE)
  $GO end
$ENDIF
 DISPLAY VISIBILITY
 GRAPH '#1 RESPONSE'
 'WAVELENGTH IN UM
 '
 '
end
}
ENTER RESPONSE TYPE (PHOTOPIC, SCOTOPIC, OR BOTH)>

PHOTOPIC_NORM {
!!NORMALIZES SPECTRAL DISTRIBUTION TO PHOTOPIC RESPONSE OF THE EYE
!!CREATES A PLOT OF THE NORMALIZATION
$ITER WAVE 380 780 -((780-380)/DLAMBDA+1) NPOWER
 {
  SELECT ONLY SOURCE (WAVE-380)/DLAMBDA+1
  FLUX 0 683*EYE(WAVE/1000) 
  STATS
  $GRAB 'TOTAL' 0 2 NPOWER
 }
SELECT ALL
}

CIEPLOT {
 #1=(#1#2)
}

CIECC10 {
!!CREATES PLOT OF CIE 1964 STANDARD OBSERVER 10 DEGREE
!!COLOR MATCHING FUNCTIONS
$READ CIE10D.INR
$ITER WAVE 380 780 -((780-380)/DLAMBDA+1) X
 $CIEPLOT X LIT[INT(WAVE)]
DISPLAY CIECC10
 GRAPH 'CIE X COLOR MATCHING FUNCTION'
 'WAVELENGTH IN NM
 'CIE 1964 STANDARD OBSERVER (10 DEGREES)
 '
$ITER WAVE 380 780 -((780-380)/DLAMBDA+1) Y
 $CIEPLOT Y LIT[INT(WAVE)]
DISPLAY CIECC10
 GRAPH 'CIE X & Y COLOR MATCHING FUNCTIONS' APPEND
 'WAVELENGTH IN NM
 'CIE 1964 STANDARD OBSERVER (10 DEGREES)
 'SOLID CURVE IS X  DOTTED CURVE IS Y
$ITER WAVE 380 780 -((780-380)/DLAMBDA+1) Z
 $CIEPLOT Z LIT[INT(WAVE)]
DISPLAY CIECC10
 GRAPH 'CIE X & Y & Z COLOR MATCHING FUNCTIONS' APPEND
 'WAVELENGTH IN NM
 'CIE 1964 STANDARD OBSERVER (10 DEGREES)
 'SOLID CURVE IS X  DOTTED CURVE IS Y  DASHED CURVE IS Z
}

CIE_COORDS {
!!COMPUTE ACTUAL CIE COORIDINATES
!!$READ CIE10D
X_CIE=0 Y_CIE=0 Z_CIE=0
$DO 380 780 (DLAMBDA)
 {
  SELECT ONLY SOURCE (?-380)/DLAMBDA+1
  STATS
  $GRAB 'TOTAL' 0 2 SPEC_POW
  X_CIE=X_CIE+X?*SPEC_POW*683
  Y_CIE=Y_CIE+Y?*SPEC_POW*683
  Z_CIE=Z_CIE+Z?*SPEC_POW*683
 }
XCIE_COORD=X_CIE/(X_CIE+Y_CIE+Z_CIE)
YCIE_COORD=Y_CIE/(X_CIE+Y_CIE+Z_CIE)
ZCIE_COORD=Z_CIE/(X_CIE+Y_CIE+Z_CIE)
$IO OUTPUT CIE_COORDS 13
$SCR 2
CIE X COORDINATE  CIE Y COORDINATE  CIE Y COORDINATE
 \XCIE_COORD.\     \YCIE_COORD.\     \ZCIE_COORD.\
$IO OUTPUT CLOSE
}

!!SET UP BLACK BODY EMITTER
&BLACKBODY
STATS
DISPLAY BLACKBODY
GRAPH 'SPECTRAL POWER DISTRIBUTION OF BLACKBODY EMITTER'
'WAVELENGTH IN NANOMETERS, POWER IN WATTS
'
'

!!SETS UP AND PLOTS CIE COLOR MATCHING FUNCTIONS
&CIECC10

!!COMPUTE CIE COLOR COORDINATES
&CIE_COORDS

!!PLOTS THE PHOTOPIC OR SCOTOPIC VISIBILITY CURVES 
&VISIBILITY BOTH

!!PHOTOPICALLY NORMALIZE THE BLACKBODY CURVE
&PHOTOPIC_NORM
STATS
DISPLAY PHOTOPIC_NORM
GRAPH 'PHOTOPIC RESPONSE OF THE BLACKBODY SOURCE'
'WAVELENGTH IN NANOMETERS, POWER IN LUMENS
'THE CURVE IS A SPECTRAL POWER DISTRIBUTION OF A PHOTOPICALLY
'NORMALIZED BLACKBODY SOURCE

BB=LPW(TEMP)
$REG BB

!!$IO

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!FLUX!!!!!!!!!!!!!!!!!!!!!!!!!
STATS
STATS POSITION
STATS DIRECTION
SELECT ONLY 1 10
LIST POSITION
SELECT ALL

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!FLUX DENSITY!!!!!!!!!!!!!!!!!!!!!!!!!
!!SPOTS POSITION: EXITANCE
WINDOW Y -2@0.5 X -2@0.5
WINDOW 1.1
PIXELS 51
SPOTS POSITION ATTRIBUTE 0 
DISPLAY 
 ISOMETRIC 'FLUX DENSITY OF A LAMBERTIAN SOURCE'
 CONTOUR 5 'CONTOUR PLOT'

!!PROJECTED FLUX DENSITY
RAYSET
ROTATE X -45 0 0
SPOTS POSITION ATTRIBUTE 0 
DISPLAY 
 ISOMETRIC 'PROJECTED FLUX DENSITY OF A LAMBERTIAN SOURCE'

RAYSET
ROTATE X 45 0 0
 
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!ANGULAR FLUX DENSITY!!!!!!!!!!!!!!!!!!!!!!!!!
!!SPOTS DIRECITON
WINDOW Y -2@SIN[90] X -2@SIN[90]
PIXELS 51
SPOTS DIRECTION ATTRIBUTE 0
DISPLAY
 ISOMETRIC 'ANGULAR FLUX DENSITY: INTENSITY IN DIRECTION COSINE SPACE'
 ANGLES
 ISOMETRIC 'INTENSITY IN ANGLE SPACE'
 CONTOUR 5 'CONTOUR PLOT OF INTENSITY' OVERLAY
 TABLE 5 5 5 PLOT

!!RADIANT
RADIANT Z 0 90 25 0 360 100
DISPLAY
 ISOMETRIC 'INTENSITY VIA RADIANT COMMAND'
 MESH
 $VIEW

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!RADIANCE!!!!!!!!!!!!!!!!!!!!!!!!
!!FIRST FORM OF RADIANT radiance COMMAND RADIANT...MAP
$IO VECTOR REWIND
WINDOW Y -2@0.5 X -2@0.5
PIXELS 2
RADIANT Z 0 90 9 0 360 36 MAP
$VIEW

!!SECOND FORM OF RADIANT radiance COMMAND RADIANT...AREA
WINDOW Y -2@0.5 X -2@0.5
WINDOW 1.1
PIXELS 51
RADIANT Z 0 20 1 0 360 1 AREA
DISPLAY
 AVERAGE
 ISO 'RADIANCE OF LAMBERTIAN EMITTER'

$DO -1 1 2
 {RAYSET
  ROTATE X ?*45 0 0
  WINDOW Y -2@0.5 X -2@0.5
  WINDOW 1.1
  RADIANT Z 0 20 1 0 360 1 AREA
  DISPLAY
   AVERAGE
   ISOMETRIC 'RADIANCE OF LAMBERTIAN EMITTER AT ?*45 DEGREES'
  RAYSET
  ROTATE X -1*?*45 0 0
 }  

RETURN