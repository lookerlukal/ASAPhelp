!!++
!! TRUE_HELIX.INR
!! Title: A Real Helix Made with Extruded Ellipses
!! Category: Simple Problem
!! Keywords: Geometry, USERCURV, helix, $FCN, extrude, curve, OBJECT, ALIGN, PLACE
!! Description: This creates a real helix by extruding many equally spaced 
!! ellipses which are aligned perpendicular to a helical USERCURV curve.
!! Edit History: (latest first)
!! 02/08/2004 - cp - formatted and added header
!! 02/06/2004 - sm - creation
!!--

SYSTEM NEW
RESET

HELRADIUS=1
PI=ACOS(-1)

$FCN NEWFUNC A=2*PI*_, x COS(A) y SIN(A) z (A)
EDGES
  USERCURV NEWFUNC -2@(0.5*PI) 61 !! Make helical curve
    SHIFT Z -1
!!  SEGMENTS 45

$DO 1 61
 {
 EDGE
   ELLIPSE Z 0 (HELRADIUS) (HELRADIUS) 16 0 360
     ALIGN 0 0 1 TAN 1 ?  !! EDGE .1, Align given vector to tangent of the curve at ?
     PLACE CURV 1 ?
 RETURN
 }

OBJECT
 -61 2 2 0 'HELIX_TUBE' !! Extrude all ellipses to make helical tube

!!PLOT EDGES OVERLAY
PLOT FACETS
$VIEW 
RETURN
