!!++
!! UNSTABLE_RESONATOR.INR
!! Title: Confocal Unstable Resonator
!! Category: Simple Problem
!! Keywords: Wave, FIELD, ENERGY, CLIP, DECOMPOSE, SPREAD, resonator
!! Description: Confocal unstable resonator showing
!! propagation of a complex beam shape.  Uses edge
!! clipping for the field calculation and DECOMPOSE
!! in POSITION. Allow some time to run. Note $IO pause below.
!! Edit History: (latest first)
!! 02/28/2002 - cp - corrected style, added BEAMS...
!! 03/16/2000 - cp - Reformatted; added header
!! 03/02/2000 - rh - creation
!!--

SYSTEM NEW
RESET

UNITS CM
BEAMS COHERENT DIFFRACT
WAVELENGTHS 10.6 UM

!! Edge used to clip the laser field
!! at the primary mirror
EDGES
  ELLIPSE Z 30 2@5 120
RETURN

!! Primary mirror of the resonator
SURFACE
  OPTICAL Z 30 -60 ELLIPSE 5
OBJECT 'PRIMARY'
  REDEFINE COLOR 1
  INTERFACE 1
SURFACE
  OPTICAL Z 10 -20 ELLIPSE 1.67
OBJECT 'SECONDARY'
  REDEFINE COLOR 1
  INTERFACE 9
SURFACE
  PLANE Z 31 ELLIPSE 5
OBJECT 'PRIMARY_BACK'
  REDEFINE COLOR 20
SURFACE
  TUBE Z 29.791 2@5 31 2@5
OBJECT 'PRIMARY_EDGE'
  REDEFINE COLOR 20
SURFACE
  PLANE Z 9 ELLIPSE 1.57
OBJECT 'SECONDARY_BACK'
  REDEFINE COLOR 20
SURFACE
  TUBE Z 9 2@1.57 9.930 2@1.67
OBJECT 'SECONDARY_EDGE'
  REDEFINE COLOR 20
SURFACE
  PLANE Z 9.9 RECTANGLE .25 6
OBJECT 'STRUTY'
  REDEFINE COLOR 3
SURFACE
  PLANE Z 9.9 RECTANGLE 6 .25
OBJECT 'STRUTX'
  REDEFINE COLOR 3
SURFACE
  PLANE Z 9.1 RECTANGLE .23 6
OBJECT 'STRUTY_BACK'
  REDEFINE COLOR 3
SURFACE
  PLANE Z 9.1 RECTANGLE 6 .23
OBJECT 'STRUTX_BACK'
  REDEFINE COLOR 3
SURFACE
  PLANE X -.23 RECTANGLE 6 .4001
OBJECT 'STRUTY_EDGE_MX'
  REDEFINE COLOR 3
  SHIFT 0 0 .4001
  ROTATE Y -1.432 0 -.23
  SHIFT 0 0 9.1
SURFACE
  PLANE X .23 RECTANGLE 6 .4001
OBJECT 'STRUTY_EDGE_PX'
  REDEFINE COLOR 3
  SHIFT 0 0 .4001
  ROTATE Y 1.432 0 .23
  SHIFT 0 0 9.1
SURFACE
  PLANE Y -.23 RECTANGLE .4001 6
OBJECT 'STRUTX_EDGE_MX'
  REDEFINE COLOR 3
  SHIFT 0 0 .4001
  ROTATE X 1.432 -.23 0
  SHIFT 0 0 9.1
SURFACE
  PLANE Y .23 RECTANGLE .4001 6
OBJECT 'STRUTX_EDGE_PX'
  REDEFINE COLOR 3
  SHIFT 0 0 .4001
  ROTATE X -1.432 .23 0
  SHIFT 0 0 9.1
SURFACE
  TUBE Z 9 2@6 10 2@6
OBJECT 'INNER_STRUT_MNT'
  REDEFINE COLOR 3
SURFACE
  TUBE Z 9 2@7 10 2@7
OBJECT 'OUTER_STRUT_MNT'
  REDEFINE COLOR 3
SURFACE
  PLANE Z 10 ELLIPSE 2@7 6/7
OBJECT 'FRONT_STRUT_MNT'
  REDEFINE COLOR 3
SURFACE
  PLANE Z 9 ELLIPSE 2@7 6/7
OBJECT 'BACK_STRT_MNT'
  REDEFINE COLOR 3

!! Make a picture of the system 

!! Dummy plane
SURFACE
  PLANE Z 5 ELLIPSE 7
OBJECT 'DUMMY'

!! Define rays
GRID ELLIPTIC Z 29 -4@1.66 7 7
  SOURCE DIRECTION 0 0 -1
WINDOW Y Z
 
!! Plot the system and trace the rays
CONSIDER EXCEPT DUMMY
PLOT FACETS 5 5 OVERLAY
CONSIDER ALL
ARROWS 3
TRACE PLOT COLOR 2
CONSIDER EXCEPT DUMMY
$VIEW

PARABASALS 4
WIDTHS 1.6
GRID ELLIPTIC Z 30 -4@5 201 201
  SOURCE DIRECTION 0 0 -1
  MOVE BY Z -10
$DO 1 3
  {
  WINDOW Y Z
  WINDOW 1.1
  PROFILES OVERLAY
  TRACE STEP +2 PLOT 100
  CONSIDER ONLY PRIMARY
  WINDOW Y -5.2 5.2 X -5.2 5.2
  PIXELS 201
  FIELD ENERGY CLIP -1
  DISPLAY
    PICTURE 'ITERATION ?'
  RETURN
  RAYS 0
  CONSIDER ALL
  DECOMPOSE POSITION
  MOVE BY Z -1 
  }

RETURN 
 
$IO  !! Continue if time permits
CONSIDER EXCEPT DUMMY
TRACE
CONSIDER ONLY 0
MOVE TO Z 9
WINDOW Y X
WINDOW 1.1
PIXELS 201
SPREAD NORMAL
DISPLAY
  PICTURE
MOVE TO Z -1000
SPREAD NORMAL
DISPLAY 
  PICTURE
MOVE TO Z -100000
WINDOW Y -40 40 X -40 40
SPREAD NORMAL
DISPLAY
  PICTURE
RETURN