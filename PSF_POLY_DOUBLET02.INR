!!++
!! PSF_POLY_DOUBLET02.INR
!! Title: Polychromatic PSF of Lens Sequence
!! Category: Simple Problem
!! Keywords: Wave, SPREAD, psf, SEQUENCE, doublet, Lenses
!! Description: The following produces a Point Spread Function of
!! an achromatic doublet made with LENSES SEQUENCE command. 
!! Energy contributions from three wavelengths are incoherently
!! summed at the target and plotted. 
!! Edit History: (latest first)
!! 11/05/2003 - cp - fixed keyword
!! 02/03/2000 - cp - creation
!!--

SYSTEM NEW
RESET

UNITS INCHES
WAVELENGTHS 486      587      656 NM

MEDIA
  1.490566 1.486577 1.484805 'FK51'
  1.572434 1.565028 1.561763 'ADF1'

LENSES 
  SEQUENCE RADI  !! Using vertex radius
    0 0 0, 0 0 1, 3 31.540766 0 0 FK51
    0 0 .8, 0 0 1, 3 -16.219535 0 0 0
    0 0 1.065884, 0 0 1, 3 -16.219535 0 0 ADF1
    0 0 1.465884, 0 0 1, 3 -98.249199 0 0 0
OBJECT 'DOUBLET'

SURFACE
  PLANE Z 60.077294 ELLIPSE 0.7 
OBJECT 'DETECTOR'

!! PSF Calculations
PARABASALS 4
BEAMS COHERENT DIFFRACT
WIDTHS 1.414

WAVELENGTH 486 NM
GRID POLAR Z -1 0 3 0 360 5 6
  SOURCE DIRECTION 0 0 1

WAVELENGTH 587 NM
GRID POLAR Z -1 0 3 0 360 5 6
  SOURCE DIRECTION 0 0 1
SELECT ONLY SOURCE 2
FLUX 0 .9345  !! Weight flux relative to 486 NM

WAVELENGTH 656 NM
GRID POLAR Z -1 0 3 0 360 5 6
  SOURCE DIRECTION 0 0 1
SELECT ONLY SOURCE 3
FLUX 0 .8389  !! Weight flux relative to 486 NM

SELECT ALL
PLOT FACETS 8 8 OVERLAY
TRACE PLOT
CONSIDER ONLY DETECTOR
WINDOW Y -2@.0015 X -2@.0015
PIXELS 61
SPREAD NORMAL -27
DISPLAY 27
  ISOMETRIC 'POLYCHROMATIC PSF OF A DOUBLET'
  FORM -6
  PLOT3D 'POLYCHROMATIC PSF OF A DOUBLET (log plot)'
RETURN
