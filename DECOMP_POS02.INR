!!++
!! DECOMP_POS02.INR
!! Title: Decomposing in Position #2
!! Category: Simple Problem
!! Keywords: Wave, DECOMPOSE, FIELD, ENERGY, TRACE STEP, CLIP
!! Description: Demonstration of decomposing in position.
!! A slightly different version from the original 
!! decomp_pos.inr.
!! Edit History: (latest first)
!! 08/18/2005 - jh - modified from decomp_pos.inr
!! 01/01/1996 - bro - creation
!!--

SYSTEM NEW
RESET

COATING PROPERTIES
  0 1 'TRANS'

EDGES 
  ELLIPSE Z 5 2@7
  RECTANGLE Z 5 1 1 16 
  OBJECT
    0.1 0.2 'APERTURE'

SURFACE
  PLANE Z 4.95 ELLIPSE 7 
  OBJECT 'DUMMY_PLANE  !! A dummy plane to stop rays at aperture
    INTERFACE COATING TRANS AIR AIR

LENS
  PERFECT Z 10 90 10
OBJECT 'FFLENS'

SURFACE
  PLANE Z 100 ELLIPSE 0.2
OBJECT 'DETECTOR' 
  
PARABASALS 4 
BEAMS COHERENT DIFFRACT
WAVELENGTHS 1E-4
WIDTHS 1.414
GAUSSIAN Z -10  -10 11 8

$GUI CHARTS_OFF
PIXELS 71
FIELD ENERGY -10 YX -2@10 -2@10
DISPLAY
  PICTURE 'Initial field'
RETURN

!! GRID ELLIPTIC Z -10 -4@6 2@20
!!   SOURCE DIR 0 0 1

TRACE STEP 1  !! Trace rays up to dummy plane
WINDOW Y -2@2 X -2@2
PIXELS 51  !! Set window and pixel for decompose
FIELD ENERGY 5 CLIP -0.4
DISPLAY 29 ENERGY  !! Create distribution file
  ISOMETRIC 'CLIPPED IRRADIANCE PATTERN'
RAYS 0  !! Zero original rays

DECOMPOSE POSITION  !! Create the spatial and angular beam set properties

FIELD ENERGY 5
DISPLAY 29 ENERGY
  ISOMETRIC 'DECOMPOSED IRRADIANCE PATTERN'
RETURN 

WINDOW Y Z
PLOT FACETS 5 5 0 OVERLAY
TRACE PLOT 11

CONSIDER ONLY DETECTOR
WINDOW Y X
WINDOW 0.2
PIXELS 111
!! WINDOW Y -2@0.003 X -2@0.003
SPREAD NORMAL 
DISPLAY 
  FORM -3
  PICTURE 'PSF OF RECTANGULAR APERTURE SYSTEM W/DECOMPOSE POSITION'
RETURN 

$GUI CHARTS_ON
