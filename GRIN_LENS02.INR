!!++
!! GRIN_LENS02.INR
!! Title: GRIN Photographic Lens
!! Category: Simple Problem
!! Keywords: GRIN, MEDIA, FUNCTION, lens
!! Description: Gradient index photographic lens.
!! Edit History: (latest first)
!! 10/10/2000 - cp - modified format; added description
!! 01/01/1996 - bro - creation
!!-- 

SYSTEM NEW
RESET

N00=1.8080   N10=-7.778E-4   N20=2.9543E-7
FUNCTION
  GENERAL
    C 1
    Y2 N10/N00
    Z2 N10/N00 
    Y4 N20/N00
    Y2Z2 2*N20/N00
    Z4 N20/N00
MEDIA
  (N00) GRIN function 1  exponent 2  stepsize 0.1 'GRIN'
COATING PROPERTIES
  0 1  'TRANS'

SURFACE
  OPTICAL X 0 79.3651 ELLIPSE 20
  OBJECT 'LENS_1A'
    INTERFACE COATING +TRANS AIR GRIN
SURFACE
  OPTICAL X 11.62 39.8406 ELLIPSE 12.5
  OBJECT 'LENS_1B'
    INTERFACE COATING +TRANS AIR GRIN
SURFACE
  PLANE X  13.58 ELLIPSE 20 20 12.5/20
  OBJECT 'STOP'
SURFACE
  TUBE X 2.52 2@20 13.58 2@20
  OBJECT 'LENS1_EDGE'
    INTERFACE COATING +TRANS AIR GRIN
SURFACE
  OPTICAL X 26.14 -46.7290 ELLIPSE 14.2
  OBJECT 'LENS_2A'
    INTERFACE COATING +TRANS AIR GRIN
SURFACE
  OPTICAL X 43.89 -163.9344 ELLIPSE 22.5
  OBJECT 'LENS_2B'
    INTERFACE COATING +TRANS AIR GRIN

SURFACE
  PLANE X 24.026 ELLIPSE 2@22.5 14.2/22.5
  OBJECT 'LENS_2_BEVEL'
    INTERFACE COATING +TRANS AIR GRIN
SURFACE
  TUBE X 24.026 2@22.5 42.35 2@22.5
  OBJECT 'LENS2_EDGE'
    INTERFACE COATING +TRANS AIR GRIN
SURFACE
  PLANE X 77.42 ELLIPSE 25
  OBJECT 'FILM_PLANE'

SEARCH ALL FORWARD

BEAMS INCOHERENT GEOMETRIC
GRID ELLIPTIC X 9.3 -4@12.5 5 1
  SOURCE DIRECTION 1 0 0, COS[20] SIN[20] 0, COS[-20] SIN[-20] 0
  MOVE TO X -10

WINDOW Y X
WINDOW 1.3
PROFILES OVERLAY 'GRIN PHOTOGRAPHIC OBJECTIVE'
TRACE PLOT
RETURN