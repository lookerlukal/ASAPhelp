!!++
!! RENDER01.INR
!! Title: Rendering Objects
!! Category: Isolated Command
!! Keywords: RENDER, Lenses
!! Description: Example of rendering a group
!! of lenses. The resolution of RENDER is
!! determined by the PIXELS command.
!! Edit History: (latest first)
!! 10/11/2000 - cp - modified format; added description
!! 01/01/1996 - bro - creation
!!--

SYSTEM NEW
RESET

MEDIA
  1.620 'SK16'
  1.620 'SCHOTTNEW_F4'
COATING PROPERTIES
  0.04 0.96 'GHOST'

SURFACE
  OPTICAL Z (T=0) 36.203 ELLIPSE 11
  OBJECT 'LENS_A_SURF_1'
    INTERFACE COATING +GHOST AIR SK16
SURFACE
  OPTICAL Z (T=T+4.5) -3521.1268 ELLIPSE 11
  OBJECT 'LENS_A_SURF_2'
    INTERFACE COATING +GHOST AIR SK16
SURFACE
  TUBE Z 1.711592 2@11 T-.0172 2@11
  OBJECT 'LENS_A_RIM'
    INTERFACE COATING +GHOST AIR SK16

SURFACE
  OPTICAL Z (T=T+8.7512) -57.9878 ELLIPSE 7
  OBJECT 'LENS_B_SURF_1'
    INTERFACE COATING +GHOST AIR SCHOTTNEW_F4
SURFACE
  OPTICAL Z (T=T+2.5) 36.2056 ELLIPSE 7
  OBJECT 'LENS_B_SURF_2'
    INTERFACE COATING +GHOST AIR SCHOTTNEW_F4
SURFACE
  TUBE Z T-2.5-.424 2@7 (T+.683) 2@7
  OBJECT 'LENS_B_RIM'
    INTERFACE COATING +GHOST AIR SCHOTTNEW_F4

SURFACE
  OPTICAL Z (T=T+9.6205) 161.2383 ELLIPSE 11
  OBJECT 'LENS_C_SURF_1'
    INTERFACE COATING +GHOST AIR SK16
SURFACE
  OPTICAL Z (T=T+6) -45.4752 ELLIPSE 11
  OBJECT 'LENS_C_SURF_2'
    INTERFACE COATING +GHOST AIR SK16

SURFACE
  TUBE Z T-6+.376 2@11 T-1.35 2@11
  OBJECT 'LENS_C_RIM'
    INTERFACE COATING +GHOST AIR SK16

SURFACE
  PLANE Z (T=T+83.7092) ELLIPSE 30
  OBJECT 'IMAGE_PLANE'
 
WINDOW Y -2@20 Z -12 45
PIXELS 275
OBLIQUE
RENDER
 
RETURN