!!++
!! DIRECTIONAL.INR
!! Title: DIRECTIONAL Display Command
!! Category: Simple Problem
!! Keywords: Analysis, DIRECTIONAL, DISPLAY, SELECT, SPOTS
!! Description: Create a DIRECTIONAL plot of selected
!! rayset created from SPOTS DIRECTION. A polar and 
!! cartesian plot is demonstrated.
!! Edit History: (latest first)
!! 11/29/2004 - cp - adjusted window range
!! 10/04/2000 - cp - modified format;
!! 01/01/1996 - bro - creation
!!--

SYSTEM NEW
RESET

FRESNEL AVERAGE
MEDIA
  2.4 'ZNSE'
SURFACE
  PLANE X 0 ELLIPSE 0.125 
  OBJECT 'ENTRANCE_FACE'
    INTERFACE COATING +BARE AIR ZNSE
SURFACE
  TUBE X 0 2@.125 8 2@.125 
  OBJECT 'FIBER_BODY'
    INTERFACE COATING -BARE AIR ZNSE
SURFACE
  PLANE X 8 ELLIPSE 0.125 
  OBJECT 'EXIT_FACE'
    INTERFACE COATING +BARE AIR ZNSE

WINDOW Z -4 4 X -1.2 9
PIXELS 21
HALT 1000
BEAMS INCOHERENT GEOMETRIC
GRID ELLIPTIC X 0 -4@.125 1 10
  SOURCE POSITION -.5 0 0
  MOVE TO X -.5
PROFILES OVERLAY 'ZnSe FIBER OPTIC w/POINT SOURCE'
TRACE PLOT

RAYS 0
GRID ELLIPTIC X 0 -4@.125 201 201
  SOURCE POSITION -.5 0 0
  MOVE TO X -.5
TRACE  

SELECT ONLY 0 A
WINDOW Y -2@.3 Z -2@.3 !! Just bigger than angular cone

SPOTS DIRECTION ATTRIBUTE 1 'ANGULAR DISTRIBUTION OF EXITING RAYS FROM ZnSe FIBER'
SPOTS DIRECTION ATTRIBUTE 0
  DISPLAY
    RADIAL
    DIRECTION 'POLAR PLOT OF RADIALLY AVERAGED INTENSITY FROM ZnSe FIBER'
    DIRECTION UNWRAP 'CARTESIAN PLOT OF RADIALLY AVERAGED INTENSITY'
RETURN
