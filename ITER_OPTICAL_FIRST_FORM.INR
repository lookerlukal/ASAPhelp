!!++
!! ITER_OPTICAL_FIRST_FORM.INR
!! Title: Modifying OPTICAL RC with $ITER
!! Category: Simple Problem
!! Keywords: Scripts, $ITER, OPTICAL, minimum, merit, iterate, redefining
!! Description: The following uses the first form of $ITER to graph the results
!! of a range of Radius of Curvatures for an OPTICAL. The $ITER records the entire
!! the entire range in ITER.DIS and leaves off where the minimum RofC was found. 
!! The OPTICAL is rebuilt with a new RofC at each iteration (the object's associated 
!! OPTICAL entity is redefined) and the XZ deviations of the the rays
!! at the target are saved at each step. This is not as accurate as using
!! the second form - see "ITER_OPTICAL_Second_Form.INR" where a more precise
!! minimum may be found.  This first form will simply step through the range
!! as given, leaving it in the best step where the minimum was found. It starts
!! with a desired focus position of 15. This is where the target
!! is placed. Change this number to view different results. The $ITER
!! R of C range should be made to cover the new Target position.
!! Another method for changing polynomial coefficients using ALTER
!! in a $ITER, may be found in ITER_GENERAL_First_Form.inr
!! Edit History: (latest first)
!! 08/05/2004 - cp - creation
!!--

SYSTEM NEW
RESET  

MEDIA
  1.5 'GLASS'  
  
FOCPOS=15 !! Change this for a new desired focus (target) position
SURFACE 
  OPTICAL Y 0 5 0 ELLIPSE 2.5 2.5
OBJECT 'CURVED_SURF'

  INTERFACE 0 1 AIR GLASS

WINDOW X Y
SURFACE
  PLANE Y (FOCPOS) RECTANGLE 10 10
OBJECT 'TARGET' 

$ITER R_CURV 3 7 -20 MERIT  !! Uses second form - Finding the minimum
    {
    RAYS 0  !! Clear old rays
    SURFACE 1 !! REDEFINES SURFACE 100
      OPTICAL Y 0 (R_CURV) 0 ELLIPSE 2.5 2.5
    OBJECT 1 'CURVED_SURF' !! Redefine Object 1 with new R_CURV
      INTERFACE 0 1 AIR GLASS
    GRID ELLIPTIC Y -10 -2 2 -2 2 10 10
      SOURCE DIRECTION 0 1 0
    TRACE
    CONSIDER ONLY TARGET
    STATS POSITION
    $GRAB 'RMS Deviation' 0 1 XDEV
    $GRAB 'RMS Deviation' 0 3 ZDEV
    MERIT=ABS(XDEV+ZDEV)  !! Find minimum deviations in X and Y
    $GRAB 'Centroid' 0 2 CENTROID
    CONSIDER ALL
    }

!! Show setup for min MERIT after ITER finishes:
RAYS 0 !! Clear old rays
GRID ELLIPTIC Y -10 -2 2 -2 2 10 10
  SOURCE DIRECTION 0 1 0 
  MISSED ARROWS 50
PLOT FACETS 5 5 0 OVERLAY
TRACE PLOT OVERLAY 
CONSIDER ONLY TARGET
SPOTS POSITION 
DISPLAY ITER  
GRAPH     
RETURN
$SCR 4  !! The following 3 lines get printed
  For Target plane at \FOCPOS.\
  Found minimum at \MERIT.\  
  R of C = \R_CURV.    \ for a Centroid Y position of \CENTROID.  \   
\DUMMY:? 
!! \DUMMY:? Causes $SCR to print in a pop up window 
RETURN 