!!++
!! TERRAIN01.INR
!! Title: Use of the TERRAIN command
!! Category: Isolated Command
!! Keywords: TERRAIN, triplet, Analysis, DISPLAY 
!! Description: TERRAIN command with first element of Cooke Triplet 
!! Used alone, it gives the peak and valley statistics with
!! FWHM at highest peak along a vertical slice of the data
!! (as visualized in ASAP using DISPLAY; PICTURE) passing
!! through the biased global maximum of the data. 
!! Edit History: (latest first) 
!! 08/22/2007 - cp - reformatted
!! 04/12/2007 - bf - creation
!!--

SYSTEM NEW
RESET

UNITS MM

MEDIA
  1.613  'SK4'

SURFACE
  OPTICAL Z 0 44.55 ELLIPSE 16.9 16.9
OBJECT 'L1.FRONT'    
  INTERFACE 0 1 AIR SK4
  
SURFACE
  OPTICAL Z 5 -436.6 ELLIPSE 16.9 16.9
OBJECT 'L1.BACK'    
  INTERFACE 0 1 AIR SK4

SURFACE
  TUBE Z 0 16.9 16.9   5 16.9 16.9
OBJECT 'L1.EDGE'
  BOUNDS  +.3  -.2

SURFACE
  PLANE Z  65.12    RECT 5
OBJECT 'DETECTOR'  

GRID ELLIPSE Z -1 -4@16 101  101
SOURCE DIRECTION 0 0 1
FLUX TOTAL 100

WINDOW Y Z
PLOT FACETS 7 7 0   OVERLAY
TRACE PLOT  111

CONSIDER ONLY DETECTOR
WINDOW X  Y  
PIXELS 101

SPOTS POSITION

DISPLAY
  AVERAGE
  PICTURE
  TERRAIN
RETURN
