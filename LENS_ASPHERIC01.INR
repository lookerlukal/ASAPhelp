!!++
!! LENS_ASPHERIC01.INR
!! Title: Aspheric Lens by Sag Coefficients 
!! Category: Simple Problem
!! Keywords: Lenses, aspheric, USERSAG, $FCN, Melles-Griot, sag
!! Description: An Aspheric lens from Melles-Griot created
!! by USERSAG, with raytrace. This method may be used
!! for lenses defined by sag coefficients rather than 
!! aspheric coefficients. 
!! Edit History: (latest first)
!! 04/09/2007 - cp - corrected keyword
!! 04/10/2002 - cp - modified format and header
!! 04/10/2002 - cl - creation
!!--

SYSTEM NEW
RESET

MEDIA
  1.5  'GLASS'
COATING PROPERTIES 
  0 1   'TRANSMIT'
ARROWS OFF

UNITS MM

!! Define geometry

!! Create aspheric surface of Melles Griot lens
A1=1.6380E-03  !! First  order sag coefficient
A2=8.4700E-02  !! Second order sag coefficient
A3=7.2934E-04  !! Third  order sag coefficient
A4=4.1096E-04  !! Fourth order sag coefficient 

$FCN MG_LENS (_*A1+_^2*A2+_^3*A3+_^4*A4)  !! Define function for sag of lens
SURFACE
  USERSAG Z 0 MG_LENS ELLIPSE 6 6  !! 0 0 0 is the reference point for the function
OBJECT 'Melles_Griot_LAG000_Surface_1'
  INTERFACE COATING +TRANS AIR GLASS

!! Create spherical surface of Melles Griot lens
SURFACE
  OPTICAL Z 5.5 -12.25 ELLIPSE 6 6
OBJECT 'Melles_Griot_LAG000_Surface_2'
  INTERFACE COATING +TRANS AIR GLASS

!! Create edge of of Melles Griot lens
SURFACE
  TUBE Z 3.749171 2@6 3.92954 2@6
OBJECT 'Melles_Griot_LAG000_Edge'
  INTERFACE COATING +TRANS AIR GLASS

!! Create a detector to catch the rays during tracing
SURFACE
  PLANE Z 10 ELLIPSE 6
OBJECT 'Detector'

!! Verify geometry
WINDOW Y Z
PLOT FACETS 3 3 OVERLAY

!! Create source
GRID ELLIPTIC Z -5 -4 4 -4 4 101 101
  SOURCE DIRECTION 0 0 1

!! Trace rays
TRACE PLOT 100

!! Analyze results
CONSIDER ONLY DETECTOR
FOCUS MOVE  !! Move the rays to the best rms focus position

WINDOW X Y
SPOTS POSITION
RETURN      



