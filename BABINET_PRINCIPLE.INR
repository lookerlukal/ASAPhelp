!!++
!! BABINET_PRINCIPLE.INR
!! Title: Babinet's Principle
!! Category: Simple Problem
!! Keywords: Wave, Babinet, spider, diffraction, SHAPE MODE, FIELD MODULUS, psf
!! Description: Babinet's principle of spider diffraction.
!! Babinet's Principle is for modeling tricky diffraction
!! problems. Set the amplitudes of certain beams to be of
!! opposite sign from other beams, so beam amplitudes are
!! in effect subtracted from one another. In this Example
!! of Babinet's Principle, thin spider vanes and a central
!! obscuration are modeled with sources whose amplitudes are
!! -1, and so their contributions to the total optical field
!! are subtracted rather than added. This is more efficient
!! than increasing the ray density in hopes that the vanes
!! are sufficiently sampled. DECOMPOSE POSITION can also be
!! used to perform this calculation.
!! Edit History: (latest first)
!! 11/05/2003 - cp - fixed keyword
!! 10/02/2000 - cp - modified format; added description
!! 01/01/1996 - bro - creation
!!--

SYSTEM NEW
RESET

UNITS IN  
D=0.04  W=1.732

COATING PROPERTIES
  1 0  'REFL'

SURFACE
  OPTICAL Z 10 -40 -1 ELLIPSE 5
OBJECT 'MIRROR'
  INTERFACE COATING REFL

SURFACE
  PLANE Z -10 ELLIPSE 1
OBJECT 'DETECTOR'

PARABASALS 4
BEAMS COHERENT DIFFRACT  !! Input
WAVELENGTHS 0.587 UM
WIDTHS (W)            
GRID POLAR Z 0 0 5 0 360 5 6
  SOURCE DIRECTION 0 0 1 

GRID POLAR Z 0 0 1.5 0 360 5 6
  SOURCE DIRECTION 0 0 1  !! Obscuration
SELECT ONLY SOURCE 0.1
SHAPE MODE 1 -1

GRID RECT Z 0 -D/W D/W 1.5 5 1 11
  SOURCE DIRECTION 0 0 1  !! Spider vanes
SELECT ONLY SOURCE 0.1
SHAPE MODE 1 -1  

GRID RECT Z 0 -D/W D/W 1.5 5 1 11
  SOURCE DIRECTION 0 0 1
SELECT ONLY SOURCE 0.1
SHAPE MODE 1 -1 

ROTATE Z 120 0 0 
GRID RECT Z 0 -D/W D/W 1.5 5 1 11
  SOURCE DIRECTION 0 0 1
SELECT ONLY SOURCE 0.1
SHAPE MODE 1 -1 
ROTATE Z -120 0 0 

TITLE 'RAYGRIDS AT ENTRANCE APERTURE'
WINDOW Y -2@6 X -2@6
PIXELS 71
SELECT ONLY SOURCE 1
  SPOTS POSITION ATTRIBUTE 1 OVERLAY
SELECT ONLY SOURCE 2
  SPOTS POSITION ATTRIBUTE 2 OVERLAY
SELECT ONLY SOURCE 3 OR SOURCE 4 OR SOURCE 5
  SPOTS POSITION ATTRIBUTE 3

SELECT ALL
FIELD MODULUS 0
DISPLAY
  ISOMETRIC 'FIELD AT ENTRANCE APERTURE SHOWING SPIDER CUTOUT'
TRACE
WINDOW Y -2@.0008 X -2@.0008
FIELD MODULUS -10
DISPLAY
  FORM -6
  ISOMETRIC 'LOG PSF SHOWING EFFECTS OF SPIDER DIFFRACTION'

WINDOW Y -2@.0008 X -2@.0008  !! Only original unobscured aperture
SELECT ONLY SOURCE 1
  FIELD MODULUS -10
DISPLAY
  FORM -6
  ISOMETRIC 'LOG PSF WITH NO SPIDER' 
RETURN

