!!++
!! PSF_POLY_DOUBLET01.INR
!! Title: Polychromatic PSF of Discreet Lens
!! Category: Simple Problem
!! Keywords: Wave, SPREAD, psf, OPTICAL, doublet, lens
!! Description: The following produces a Point Spread Function of
!! an achromatic doublet made with discreet optical surfaces. 
!! Three wavelengths are summed incoherently at the target and 
!! plotted.
!! Edit History: (latest first) 
!! 11/05/2003 - cp - fixed keyword
!! 02/03/2000 - cp - creation
!!--

SYSTEM NEW
RESET

WAVELENGTHS 486 587 656 UNITS 39.37E-9
MEDIA
  1.490566 1.486577 1.484805 'FK51'
  1.572434 1.565028 1.561763 'ADF1'

SURFACE
  OPTICAL Z 0 31.540766
    LOCAL -4@3 -.1 .3 Z
OBJECT 'LENS1SURF1'
  INTERFACE 0 1 0 FK51
SURFACE
  OPTICAL Z 0.8 -16.219535; LOCAL -4@3 -.5 .1 Z
OBJECT 'LENS1SURF2'
  INTERFACE REPEAT  !! Repeat previous interface assignment
SURFACE
  TUBE Z .143 2@3 .520 2@3
OBJECT 'LENS 1 EDGE'
  
SURFACE
  OPTICAL Z 1.065884 -16.219535; LOCAL -4@3 -.5 .1 Z
OBJECT 'LENS2SURF1'
  INTERFACE 0 1 0 ADF1
SURFACE
  OPTICAL Z 1.465884 -98.249199; LOCAL -4@3 -.1 .1 Z
OBJECT 'LENS2SURF2'
  INTERFACE REPEAT
SURFACE
  TUBE Z .786 2@3 1.420 2@3
OBJECT 'LENS2EDGE'

SURFACE
  PLANE Z 60.077294; LOCAL -4@.7 -.1 .1 Z
OBJECT 'DETECTOR'

!! PSF calculations
PARABASAL 4
BEAMS COHERENT DIFFRACT
WIDTHS 1.414
PIXELS 61

WAVELENGTH 486
GRID POLAR Z -1 0 3 0 360 5 6
  SOURCE DIRECTION 0 0 1

WAVELENGTH 587
GRID POLAR Z -1 0 3 0 360 5 6
  SOURCE DIRECTION 0 0 1
SELECT ONLY SOURCE 2
  FLUX 0 .9345 !! 5900 degree K blackbody weighting relative to 486 nm

WAVELENGTH 656
GRID POLAR Z -1 0 3 0 360 5 6
  SOURCE DIRECTION 0 0 1
SELECT ONLY SOURCE 3
  FLUX 0 .8389 !! 5900 degree K blackbody weighting relative to 486 nm 

SELECT ALL  
WINDOW Y Z
PLOT FACETS 8 8 OVERLAY
TRACE PLOT
CONSIDER ONLY DETECTOR
 
WINDOW Y -2@.0015 X -2@.0015
SPREAD NORMAL -27

DISPLAY 27
  ISOMETRIC 'Polychromatic PSF of doublet lens system'
  FORM -6 !! Put into log space
  PLOT3D 'Polychromatic PSF of doublet lens system (log plot)'
RETURN
