!!++
!! PTF_POLY_DOUBLET.INR
!! Title: Polychromatic PTF of Doublet
!! Category: Isolated Command
!! Keywords: Wave, SPREAD, ptf, mtf, SEQUENCE, doublet, lens 
!! Description: Polychromatic PTF of a doublet lens.
!! Edit History: (latest first)
!! 11/05/2003 - cp - fixed keyword
!! 10/10/2000 - cp - modified format; added description
!! 01/01/1996 - bro - creation
!!-- 

SYSTEM NEW
RESET

UNITS INCHES
WAVELENGTHS 486  587  656 NM
MEDIA
  1.490566 1.486577 1.484805 'FK51'
  1.572434 1.565028 1.561763 'ADF1'

LENSES
  SEQUENCE RADI
    0 0 0, 0 0 1, 3 31.540766 0 0 FK51
    0 0 .8, 0 0 1, 3 -16.219535 0 0 AIR
    0 0 1.065884, 0 0 1, 3 -16.219535 0 0 ADF1
    0 0 1.465884, 0 0 1, 3 -98.249199 0 0 AIR
OBJECT 'DOUBLET'

SURFACE
  PLANE Z 60.077294 ELLIPSE 0.7
OBJECT 'DETECTOR'

PTF { 1  !! Polychromatic PTF calculations
!! Set up window and pixel sizes
LOWAVE=486 UNITS=39.37E-9 FNUM=10 NPIXEL=89 GIH=60*TAN[#1] Q=0.001547
WAVELENGTHS (LOWAVE) UNITS (UNITS) 
WIDE=NPIXEL*NPIXEL*LOWAVE*UNITS*FNUM/1024
WINDOW Y -WIDE+GIH WIDE+GIH X -2@(WIDE)
PIXELS (NPIXEL)

!! Set up beams
PARABASALS 4
BEAMS COHERENT DIFFRACT
WIDTHS 1.414 
WAVELENGTH 486
GRID POLAR Z 0 0 3 0 360 5 6
  SOURCE DIRECTION 0 TAN[#1] 1 
WAVELENGTH 587
GRID POLAR Z 0 0 3 0 360 5 6
  SOURCE DIRECTION 0 TAN[#1] 1 
WAVELENGTH 656
GRID POLAR Z 0 0 3 0 360 5 6
  SOURCE DIRECTION 0 TAN[#1] 1 
  MOVE TO Z -5 

TRACE
SPREAD NORMAL
DISPLAY
  FFT
  NORM MAX MAX
  NORM 1 0.01  
  GRAPH
  TRANSPOSE
  GRAPH APPEND  !! Tan and sag MTFs
DISPLAY
  FFT WAVEFRONT
  NORM 1 MAX
  NORM 1 0.01 
  GRAPH 0.5 APPEND  !! Tan PTF
DISPLAY
  TRANSPOSE
  FFT WAVEFRONT
  NORM 1 MAX
  NORM 1 0.01 
$IO PLOT REWIND
GRAPH 0.5 APPEND 'TANG/SAG MTF & PTF OF DOUBLET #1 DEG OFF-AXIS'
  'RELATIVE SPATIAL FREQUENCY [%]'
  '1 = TAN MTF / 2 = SAG MTF / 3 = TAN PTF / 4 = SAG PTF'
RETURN
}
ENTER FIELD ANGLE IN DEGREES>

&PTF 0.5
RETURN