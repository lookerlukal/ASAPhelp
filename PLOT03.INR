!!++
!! PLOT03.INR
!! Title: Plotting Lenses
!! Category: Isolated Command
!! Keywords: PLOT, Lenses
!! Description: ASAP model of Petzval lens
!! to demonstrate plotting.
!! Edit History: (latest first)
!! 10/10/2000 - cp - modified format; added description
!! 01/01/1996 - bro - creation
!!-- 

SYSTEM NEW
RESET

UNITS IN
WAVELENGTHS  486 587 656 NM
COATING PROPERTIES
  0 0 0 0 0 0 'ABSORB'

LENSES 
  SEQUENCE  RADI 
    0 0 0, 0 0 1,  1.33 2.74253 0 0 SCHOTTNEW_K7 
    1      1.2  -2.5902  0 0 SCHOTTNEW_LF5 
    .25    1.02 8.92077  0 0 AIR
    .78853 .85  0        0 0 AIR 
    .78853 .9   2.91486  0 0 SCHOTTNEW_LF5 
    .2     .9   1.57900  0 0 AIR 
    .25    1.1  1.92094  0 0 SCHOTTNEW_K7 
    .8     1.1  31.11032 0 0 AIR 

  OBJECT 'PETZVAL_LENS' 
LENSES 
  MIRROR Z  7.341469 1.811735  
  OBJECT 'DETECTOR'
    INTERFACE COATING ABSORB  

BEAMS INCOHERENT GEOMETRIC
GRID ELLIPTIC Z 2.1756 -4@.9 1 7
  SOURCE DIRECTION 0 0 1, 0 TAN[10] 1, 0 TAN[15] 1; MOVE TO Z -1 

WINDOW Y Z
WINDOW 1.3
PIXELS 127
PROFILES 'PETZVAL LENS'

PLOT LENS OVERLAY 'PETZVAL LENS W/RAYS'; TRACE PLOT
WINDOW Y Z
OBLIQUE
PLOT LENSES 'OBLIQUE (Y-Z) VIEW OF PETZVAL LENS'
WINDOW Y X
OBLIQUE
PLOT LENSES 'OBLIQUE (Y-X) VIEW OF PETZVAL LENS'
RETURN