!!++
!! DVD_PLAYER.INR
!! Title: DVD Player
!! Category: Simple Problem
!! Keywords: Wave, FIELD, cvf, laser
!! Description: DVD optical system illustrates method of
!! exporting complex vector field measured at DVD
!! landing to Lumerical FDTD Solutions file format.
!! This example requires the OPTICAL module. 
!! Edit History: (latest first)
!! 3/04/2005 - sm - creation
!!--

SYSTEM NEW
RESET
UNITS MM
LAMBDA=0.78  !! Wavelength in MICRONS

WAVELENGTHS (LAMBDA) MICRONS
MEDIA
  1.5111833 'BK7'
  1.48617   'PMMA'
  2.63`8.6  'ALUMINUM'
RETURN

FRESNEL BOTH

COATING PROPERTIES
  0.5 0.5 'BSCOAT'
RETURN

!! Beamsplitter
ENT OBJECTS
  PLANE Z 0.0 RECTANGLE 2@1.5 'BEAMSPLITTER.Z1'
    INTERFACE COATING +BARE AIR BK7
    REDEFINE COLOR 2
  PLANE Z 1.5 RECTANGLE 2@1.5 'BEAMSPLITTER.TOP'
    INTERFACE COATING +BARE BK7 AIR
    ROTATE X 90
    SHIFT Y 1.5
    REDEFINE COLOR 2
  PLANE Z 1.5 RECTANGLE 2@1.5 'BEAMSPLITTER.LEFT_SIDE'
    INTERFACE COATING +BARE BK7 AIR
    ROTATE Y 90
    SHIFT X 1.5
    REDEFINE COLOR 2
  PLANE Z 1.5 RECTANGLE 2@1.5 'BEAMSPLITTER.BOTTOM'
    INTERFACE COATING +BARE BK7 AIR 
    ROTATE X 90
    SHIFT Y -1.5
    REDEFINE COLOR 2
  PLANE Z 1.5 RECTANGLE 2@1.5 'BEAMSPLITTER.RIGHT_SIDE'
    INTERFACE COATING +BARE BK7 AIR
    ROTATE Y 90
    SHIFT X -1.5
    REDEFINE COLOR 2
  PLANE Z 3.0 RECTANGLE 2@1.5 'BEAMSPLITTER.Z3'
    INTERFACE COATING +BARE BK7 AIR
    REDEFINE COLOR 2
  PLANE Z 0.0 RECTANGLE 1.5 1.5/COS[45] 'BEAMSPLITTER.Z2'
    INTERFACE COATING BSCOAT BK7 BK7
    REDEFINE COLOR 2
    ROTATE X 45 0 0
    SHIFT Z 1.5
    SPLIT 2
GROUP BEAMSPLITTER.?
  SHIFT Z 5.0
RETURN

!! First lens
ENT OBJECTS
  OPTICAL Z 0.0 5.826832 -2.18794 ELLIPSE 2@3.001 'LENS1.FRONT'
    INTERFACE COATING +BARE "AIR" "PMMA"
    REDEFINE COLOR     3
  PLANE   Z 2.0 ELLIPSE 2@3.001 'LENS1.BACK'
    INTERFACE COATING +BARE "PMMA" "AIR"
    REDEFINE COLOR     3
  TUBE Z 0.719965 2@3.001 2.0 2@3.001 'LENS1.EDGE'
    REDEFINE COLOR     3
GROUP LENS1.?
  SHIFT Z 13.0
RETURN
 
!! Second lens
ENT OBJECTS
  OPTICAL Z 0.0 2.442745 ELLIPSE 2@2.251 'LENS2.FRONT'
    INTERFACE COATING +BARE "AIR" "PMMA"
    REDEFINE COLOR     4
  OPTICAL Z 2.0 -9.632447 0.0 0.022195 -0.001511 2.4438535E-04 ELLIPSE 2@2.001 'LENS2.BACK'
    INTERFACE COATING +BARE "PMMA" "AIR"
    REDEFINE COLOR     4
  TUBE Z 1.49406 2@2.251 2.111493 2@2.251 'LENS2.EDGE'
    REDEFINE COLOR     4
  PLANE   Z 2.111493 ELLIPSE 2@2.251 2.001/2.251 'LENS2.FLAT'
    REDEFINE COLOR     4
GROUP LENS2.?
  SHIFT  Z  18.0
RETURN

!! CD
ZCD=23.15        CDTHK=0.150   CD_DIAM=120   HOLE_DIAM=15
ENT OBJECT
  PLANE Z (ZCD-CDTHK) ELLIPSE 2@(CD_DIAM/2) HOLE_DIAM/CD_DIAM 'CD.POLY'
    INTERFACE COAT +BARE PMMA AIR
    REDEFINE COLOR 5
RETURN

ENT OBJECT
  PLANE Z (ZCD) ELLIPSE 2@(CD_DIAM/2) HOLE_DIAM/CD_DIAM 'CD.METAL'
    INTERFACE COAT BARE PMMA ALUMINUM
    REDEFINE COLOR 3
RETURN

ENT OBJECT
  TUBE Z (ZCD-CDTHK) 2@(CD_DIAM/2) (ZCD) 2@(CD_DIAM/2) 2@0 'CD.EDGE.OUTER'
ENT OBJECT
  TUBE Z (ZCD-CDTHK) 2@(HOLE_DIAM/2) (ZCD) 2@(HOLE_DIAM/2) 2@0 'CD.EDGE.INNER'

GROUP CD.?
  SHIFT X -(CD_DIAM/2)+5
RETURN

!! Detector focusing lens
ZOFFSET=6.5
ENT OBJECTS
  OPTICAL Z 0.0 2.442745 ELLIPSE 2@2.251 'LENS3.FRONT'
    INTERFACE COATING +BARE "AIR" "PMMA"
    REDEFINE COLOR     4
  OPTICAL Z 2.0 -9.632447 0.0 0.022195 -0.001511 2.4438535E-04 ELLIPSE 2@2.001 'LENS3.BACK'
    INTERFACE COATING +BARE "PMMA" "AIR"
    REDEFINE COLOR     4
  TUBE Z 1.49406 2@2.251 2.111493 2@2.251 'LENS3.EDGE'
    REDEFINE COLOR     4
  PLANE   Z 2.111493 ELLIPSE 2@2.251 2.001/2.251 'LENS3.FLAT'
    REDEFINE COLOR     4
GROUP LENS3.?
  ROTATE X 90 0 0
  SHIFT 0 -3 (ZOFFSET)
RETURN

!! Detector
ENT OBJECT
  PLANE Y -5.682896 RECTANGLE 1 'DET' 
    SHIFT Z (ZOFFSET)
RETURN

BEAMS COHERENT DIFFRACT
PARABASALS 4
WIDTHS 1.6

!! Laser diode parameters
ZP=0  !! Laser diode waist location
XRAD=0.1  YRAD=0.04  !! Divergence half-angles in radians
POLARIZ X 1 0
GAUSSIAN Z 0.1 2@(ZP) 2@1 2@0 (XRAD) (YRAD) RADIANS
FIELD ENERGY (ZP) YX -2@0.008 -2@0.004 PIXELS 51
DISPLAY
  PICTURE 'laser diode'
RETURN

!! DECOMPOSE for all three polarizations
RAYS 0
POLARIZ X
  DECOMPOSE DIRECTION .99 10
POLARIZ Y
  DECOMPOSE DIRECTION .99 10
POLARIZ Z
  DECOMPOSE DIRECTION .99 10

!! TRACE rays to cd metal layer surface
WINDOW Y Z
PLOT FACETS 5 5 0 OVERLAY
TRACE 0 -CD.METAL PLOT 50
$VIEW

!! Calculate FIELD above landing
CONSIDER ONLY CD.METAL
FIELD ENERGY 23.149 YX -4@0.004 PIXELS 121
DISPLAY
  PICTURE '1 micron above DVD landing'
RETURN

!! Export field to FDTD Solutions field format
CVF EXPORT LUMERICAL BRO029.DAT FDTDFIELD.FLD