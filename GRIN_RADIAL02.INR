!!++
!! GRIN_RADIAL02.INR
!! Title: Radial GRIN Fiber
!! Category: Simple Problem
!! Keywords: GRIN, MEDIA, FUNCTION, radial, fiber
!! Description: Example of a GRIN refractive material.
!! Trace a collimated source of rays through the material
!! and do various plots of fiber and rays.
!! Edit History: (latest first)
!! 10/10/2000 - cp - modified format; added description
!! 01/01/1996 - bro - creation
!!-- 

SYSTEM NEW
RESET

UNITS MM
WAVELENGTHS 0.6328 UM

A=0.247  A0=-A*A/2  !! Quadratic index term
FUNCTION
  GENERAL 0 0 0
    C 1
    X2 (A0)
    Y2 (A0)
MEDIA 
  1.5637 GRIN surf 1 exponent 2 stepsize .1 mm 'SELFOC'

COATING PROPERTIES
  0 1 'TRANS'
SURFACE
  PLANE Z 0 ELLIPSE 2
  OBJECT 'FIBER_FACE 
    INTERFACE COATING TRANS AIR SELFOC
SURFACE
  TUBE Z 0 2@2 20 2@2
  OBJECT 'FIBER_BODY' 
    INTERFACE COATING TRANS AIR SELFOC
SURFACE
  PLANE Z 20 ELLIPSE 2
  OBJECT 'FIBER_END' 
    INTERFACE COATING TRANS AIR SELFOC
SURFACE
  PLANE Z 29 RECTANGLE 4
  OBJECT 'DETECTOR' 

BEAMS INCOHERENT GEOMETRIC
GRID ELLIPSE Z 0 -4@.75 1 10
  SOURCE POSITION 0 0 -1
  MOVE TO Z -1

WINDOW Y -5 5  Z -2 30
PIXELS 127
PROFILES 'YZ SLICE THROUGH FIBER'
WINDOW X Z
PROFILES 'XZ SLICE THROUGH FIBER'
CONSIDER ONLY 2 4
WINDOW X Z
OBLIQUE
PLOT LIMITS 'PLOT LIMIT BOX BOUNDARIES'
CONSIDER ALL
WINDOW Y Z
OBLIQUE OFF
TRACE PLOT 'PLOT RAYS'
OBLIQUE 
WINDOW Y X
WINDOW 1.2
!! Hit right mouse button on plot to end replot
REPLOT 'NIPPON SLS 2.0 GRADIENT INDEX ROD' 
RETURN
