!!++
!! APOD_DIR_FCN.INR
!! Title: Apodizing in Direction using $FCN
!! Category: Simple Problem
!! Keywords: Sources, USERAPOD, apodizing, function, $FCN, Gaussian
!! Description: How to apodize a source in direction with a user
!! defined function. Uses USERAPOD with a $FCN that describes a
!! gaussian function. Since USERAPOD is used in direction space,
!! the function variables _1 and _2 are the dir-cosines for X and
!! Y respectively.
!! Edit History: (latest first)
!! 02/28/2000 - cp - modified format; added description
!! 01/18/2000 - rh - creation
!!--

SYSTEM NEW
RESET 

SEED 1 QUASI
XMEMORY MIN

$FCN GDIS GAUS[SQRT(_1^2+_2^2)]
USERAPOD DIRECTION GDIS  !! Try without the GDIS. You should get the same
                   !! results, since default for USERAPOD is Gaussian.
                  
!! For making the ray plot (100 is enough)
EMITTING RECTANGLE Z 0 2@.01 100 ISO 
WINDOW Y -1 1 X -1 1
SPOTS POSITION OVERLAY 
ARROWS OFF
PLOT RAYS 1 OVERLAY  !! Ray lengths represent flux, relative to 1

!! For making spots on the sphere (40000 is good)
RAYS 0  !! Clear previous rays
EMITTING RECTANGLE Z 0 2@.01 40000 ISO

SURFACE  !! The unit sphere
  ELLIPSOID 3@1 3@0 
  OBJECT 'UNIT_SPHERE'
   
PLOT FACETS 4 5 OVERLAY
SPOTS DIRECTION  !! Always plots on surface of unit sphere
$VIEW  !! Use axis and LitWireFrame on the Sphere
DISPLAY
  AVERAGE
  ISOMETRIC
RETURN
