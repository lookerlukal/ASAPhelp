!!++
!! HEADER_CHANGE.INR
!! Title: Changing a Dump File Wavelength
!! Category: Simple Problem
!! Keywords: Tracing, HEADER, WAVELENGTH, EMITTING, DUMP, GET, WRITE
!! Description: Shows how to modify a display file header
!! to change the wavelength setting.  A ray file created 
!! with a DUMP command has the wavelength of the source
!! saved in the header of the file.  If these rays are
!! used again for further ray tracing, they will remain
!! at that wavelength.  A new WAVELENGTH command before 
!! EMITTING DATA will not change that.  The Header of the
!! file needs modified to place a 0 in the first wavelength
!! number.  This allows the wavelength to be changed by 
!! the next WAVELENGTHS command.  Of course the wavelength
!! could be specified directly, but the former method
!! allows the option of automating the process with a macro. 
!! Edit History: (latest first)
!! 06/28/2000 - cp - creation
!!--

SYSTEM NEW
RESET

UNITS MM
WAVELENGTH 500 NM

EMITTING RECTANGLE Z 0 2@10 100 2@20
GET 
&REG WAVELEN  !! Print current wavelength of source

DUMP TESTRAYS !! Make a ray file

DISPLAY TESTRAYS        !! Bring up ray file info - see output console
HEADER _ _ _ _ _ _ _ 0  !! Set minimum wavelength range to 0
WRITE  !! Updates header information only 

RAYS 0  !! Clear old rays

WAVELENGTH 450 NM  !! Set new wavelength
EMITTING DATA TESTRAYS  !! Bring up old set of rays

!! The following verifies that the wavelength is at
!! the new setting.
GET 
&REG WAVELEN  !! Print new wavelength
DUMP TESTRAYS2
DISPLAY TESTRAYS2  !! To verify header change
RETURN

