!!++
!! ITER_SINGLET02.INR
!! Title: $ITER for bending, second form
!! Category: Isolated Command
!! Keywords: $ITER, iterate, SINGLET, bending
!! Description: Optimize singlet bending with the second
!! form of $ITER.  This form uses singular value 
!! decomposition (SVD) to seek the minimum of a function.
!! The function minimized here is the RMS blur diameter, as
!! reported by the FOCUS command, as a function of bending.
!! See also "ITER_SINGLET01.INR" for comparison with $ITER
!! as a grid search (form 1), and "OPTIMIZE_SINGLET.INR"
!! for a comparison using MINIMIZE for conic sequences.
!! Edit History: (latest first)
!! 10/07/2003 - cp - changed category
!! 08/08/2001 - cp - modified file names
!! 03/29/2001 - rh - creation
!!--

SYSTEM NEW
RESET

!! This macro performs a single iteration.  Because the
!! third numerical argument (d=0.25) is a floating point
!! number, ASAP will find the minimum using SVD.  This
!! parameter is the fractional derivative increment 
!! relative to the range specifed 

RMSMIN {   
  $ITER BN -1.0 1.0 0.05 RMSB
   {
    SYSTEM NEW
    RESET
    
    LENS
      SINGLET Z 0  1 5 SCHOTT_BK7 FL 100 (BN)
    OBJECT 'L1'
      INTERFACE 0 1 AIR AIR
    
    GRID ELLIPSE Z -1  -4@5  101 101 
    SOURCE DIRECTION Z
    
    TRACE
    FOCUS
    $GRAB 'RMS Blur Diameter =' 0 1 RMSB
   }
}
 
!! Now loop through several times, using SVD to seek the
!! minimum of the RMSB function.
  
BN=0  !! Starting Bending value
  $DO 1 4
     &RMSMIN
      
&REG BN 'Optimum bending: '


